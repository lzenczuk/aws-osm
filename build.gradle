buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.30"
    }
}

repositories {
    mavenCentral()
}


group 'com.github.lzenczuk.osm'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jp.classmethod.aws.s3'
apply plugin: 'jp.classmethod.aws.cloudformation'

allprojects{
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    compile group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.11.109'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-log4j', version: '1.0.0'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'


    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task buildAppZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
    archiveName = "app.zip"
}

task uploadAppZipToS3(type: jp.classmethod.aws.gradle.s3.SyncTask, dependsOn: buildAppZip) {
    bucketName 'aws-osm-app'
    source file('build/distributions')
}

